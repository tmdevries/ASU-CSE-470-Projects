<!DOCTYPE html>
<html>
<head>
<meta http-equiv="Content-Type" content="text/html;charset=utf-8" >
<title>Assignment 5 - Nyan Cat</title>

<!-- Code below is the vertex-shader ---------------------- -->
<script id="vertex-shader" type="x-shader/x-vertex">
attribute vec3 aPosition;
attribute vec2 aTexCoord;
attribute vec3 aNormal;

varying vec2 vTexCoord;
varying vec3 vTNormal;
varying vec4 vPosition;

uniform mat4 MVMatrix;
uniform mat4 PMatrix;
uniform mat3 NMatrix;

void
main()
{
	// Get vertex positions
	vPosition = MVMatrix * vec4(aPosition, 1.0);
    gl_Position = PMatrix * vPosition;

    // Set up texture coordinates
    vTexCoord = aTexCoord;

    // Set up for light
    vTNormal = NMatrix * aNormal;
    
}
</script>


<!-- Code below is the fragment-shader -------------------- -->
<script id="fragment-shader" type="x-shader/x-fragment">

precision mediump float;

varying vec2 vTexCoord;
varying vec3 vTNormal;
varying vec4 vPosition;

// Lighting uniforms
uniform vec3 uCameraPosition;
uniform vec3 uLightPosition;
uniform vec3 uAmbientColor;
uniform vec3 uDiffuseColor;
uniform vec3 uSpecularColor;
uniform float uShininess;

// Textures
uniform sampler2D uTextureImg;
uniform vec4 uColor;
uniform bool uUseTextureImg;
uniform bool uRainbowEffect;
uniform float uRainbowStep;

void
main()
{
	// Light
	vec3 phongLight;
	vec3 normal = normalize(vTNormal);
    vec3 lightDirection = normalize(uLightPosition - vPosition.xyz);
    
    // Diffuse
	float diffuseContribution = max(dot(normal, lightDirection), 0.0);

	// Specular
    vec3 eyeDirection = normalize(uCameraPosition-vPosition.xyz); // depends on direction of camera
    vec3 reflectionDirection = reflect(-lightDirection, normal);
    float specularContribution = pow(max(dot(reflectionDirection, eyeDirection), 0.0), uShininess);

    // Final lighting
    phongLight = uAmbientColor + uDiffuseColor * diffuseContribution + uSpecularColor * specularContribution;

    // Final color
    vec4 fragmentColor;
    if (!uRainbowEffect) {
        if (uUseTextureImg) {
        	fragmentColor = texture2D(uTextureImg, vec2(vTexCoord.s, vTexCoord.t));
        } else {
        	fragmentColor = uColor;
        }
        gl_FragColor = vec4(fragmentColor.rgb * phongLight, fragmentColor.a);
    } else {
        float PI = 3.14159;
        // because the texture coordinate t varies from 0 to 1 and the sine curve oscillates 
        // between 0 and 2*pi. I use t because my rainbow had to rotate to be in the location 
        // I wanted it so t is now the "x" axis
        float x = vTexCoord.t*2.0*PI; 
        // my sine curve, amplitude = 0.1 (to make it "shorter"). A step is added to animate the
        // rainbow. rainbowStep is managed in objectDraw.js
        float y = 0.1*sin(2.0*x+uRainbowStep)+0.4; 
        // vary the translations of the curve in order to get different thicknesses of the bands that will be created
        if (vTexCoord.s < y + 0.3 && vTexCoord.s > y + 0.2) {
            // purple
            fragmentColor = vec4(0.4, 0.2, 1.0, 1.0);
        } else if (vTexCoord.s < y + 0.2 && vTexCoord.s > y + 0.1) {
            // blue
            fragmentColor = vec4(0.0, 0.6, 1.0, 1.0);            
        } else if (vTexCoord.s < y + 0.1 && vTexCoord.s > y) {
            // green
            fragmentColor = vec4(0.2, 1.0, 0.0, 1.0);
        } else if (vTexCoord.s < y && vTexCoord.s > y - 0.1) {
            // yellow
            fragmentColor = vec4(1.0, 1.0, 0.0, 1.0);
        } else if (vTexCoord.s < y - 0.1 && vTexCoord.s > y - 0.2) {
            // orange
            fragmentColor = vec4(1.0, 0.6, 0.0, 1.0);
        } else if (vTexCoord.s < y - 0.2 && vTexCoord.s > y - 0.3) {
            // red
            fragmentColor = vec4(1.0, 0.0, 0.0, 1.0);
        } else {
            discard; // don't write the pixel because it's not a part of the rainbow
        }
        gl_FragColor = fragmentColor; // no lighting effects if it's the rainbow
    }
     
}
</script>

<script type="text/javascript" src="../Common/webgl-utils.js"></script>
<script type="text/javascript" src="../Common/initShaders.js"></script>
<script type="text/javascript" src="../Common/MV.js"></script>
<script type="text/javascript" src="Assign5.js"></script>
<script type="text/javascript" src="meshData.js"></script>
<script type="text/javascript" src="modelData.js"></script>
<script type="text/javascript" src="objectDraw.js"></script>
<script type="text/javascript" src="lighting.js"></script>
<script type="text/javascript" src="animation.js"></script>

</head>

<body onkeypress="handleKeyPress(event)">
<canvas id="gl-canvas" width="512" height="512">
Oops ... your browser doesn't support the HTML5 canvas element
</canvas>
<audio controls>
  <source src="nyan.mp3" type="audio/mp3">
Your browser does not support the audio element.
</audio>
</body>
</html>
